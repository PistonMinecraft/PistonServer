plugins {
    id 'org.checkerframework' version '0.5.19' apply false
    id 'io.freefair.lombok' version '5.3.0' apply false
}

ext {
    ENV = System.getenv()
    MC_VERSION = '1.16.5'
    VERSION = '0.1'
    BUILD = "${ENV.GITHUB_RUN_NUMBER ? 'build' : 'INTERNAL_BUILD'}${ENV.GITHUB_RUN_NUMBER ?: ''}"
}

allprojects {
    group = 'org.pistonmc'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.checkerframework'
    apply plugin: 'io.freefair.lombok'

    lombok {
        config['lombok.sneakyThrows.flagUsage'] = 'error'
        config['lombok.equalsAndHashCode.flagUsage'] = 'error'
    }

    version = "${rootProject.ext.MC_VERSION}-${rootProject.ext.VERSION}+${rootProject.ext.BUILD}"

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
        options.compilerArgs.addAll(['-Xplugin:Manifold'])
    }

    checkerFramework {
        excludeTests = true
        checkers = [
            'org.checkerframework.checker.nullness.NullnessChecker',
            'org.checkerframework.checker.units.UnitsChecker'
        ]
    }

    repositories {
        mavenCentral()
        maven {
            name 'Spigot Public Repo'
            url 'https://hub.spigotmc.org/nexus/content/groups/public'
        }
        maven {
            name 'forge-repo'
            url 'https://files.minecraftforge.net/maven/'
        }
    }

    configurations {
        testImplementation.extendsFrom annotationProcessor
    }

    dependencies {
        implementation libs.bundles.manifold.resources

        annotationProcessor libs.bundles.manifold.processors
    }
}