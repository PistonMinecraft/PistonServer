plugins {
    id 'org.checkerframework' version '0.5.16' apply false
}
ext {
    ENV = System.getenv()
    MC_VERSION = '1.16.5'
    VERSION = '0.1'
    BUILD = "${ENV.GITHUB_RUN_NUMBER ? 'build' : 'INTERNAL_BUILD'}${ENV.GITHUB_RUN_NUMBER ?: ''}"
}
allprojects {
    group = 'io.github.pistonmc'
}
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.checkerframework'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
            vendor = JvmVendorSpec.ADOPTOPENJDK
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
        options.compilerArgs.addAll(['-Xplugin:Manifold'])
    }

    checkerFramework {
        excludeTests = true
        checkers = [
            'org.checkerframework.checker.nullness.NullnessChecker',
            'org.checkerframework.checker.units.UnitsChecker'
        ]
    }

    repositories {
        mavenCentral()
        maven {
            name 'Minecraft Libraries'
            url "https://libraries.minecraft.net"
        }
        maven {
            name 'Spigot Public Repo'
            url 'https://hub.spigotmc.org/nexus/content/groups/public'
        }
    }
    configurations {
        testImplementation.extendsFrom annotationProcessor
    }
    dependencies {
        implementation "systems.manifold:manifold-rt:$LIB_MANIFOLD_VERSION"
        implementation "systems.manifold:manifold-ext-rt:$LIB_MANIFOLD_VERSION"

        annotationProcessor "systems.manifold:manifold:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-preprocessor:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-exceptions:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-strings:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-ext:$LIB_MANIFOLD_VERSION"
    }
}