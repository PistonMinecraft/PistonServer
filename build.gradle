plugins {
    id 'org.checkerframework' version '0.5.19-SNAPSHOT' apply false
}

ext {
    ENV = System.getenv()
    MC_VERSION = '1.16.5'
    VERSION = '0.1'
    BUILD = "${ENV.GITHUB_RUN_NUMBER ? 'build' : 'INTERNAL_BUILD'}${ENV.GITHUB_RUN_NUMBER ?: ''}"
}

allprojects {
    group = 'org.pistonmc'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.checkerframework'

    version = "${rootProject.ext.MC_VERSION}-${rootProject.ext.VERSION}+${rootProject.ext.BUILD}"

    java.toolchain.languageVersion = JavaLanguageVersion.of(11)

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
        options.compilerArgs.addAll(['-Xplugin:Manifold'])
    }

    compileJava {
        options.release = 8
    }

    checkerFramework {
        excludeTests = true
        checkers = [
            'org.checkerframework.checker.nullness.NullnessChecker',
            'org.checkerframework.checker.units.UnitsChecker'
        ]
    }

    repositories {
        mavenCentral()
        maven {
            name 'Spigot Public Repo'
            url 'https://hub.spigotmc.org/nexus/content/groups/public'
        }
        maven {
            name 'forge-repo'
            url 'https://files.minecraftforge.net/maven/'
        }
    }

    configurations {
        testImplementation.extendsFrom annotationProcessor
    }

    dependencies {
        implementation "systems.manifold:manifold-rt:$LIB_MANIFOLD_VERSION"
        implementation "systems.manifold:manifold-ext-rt:$LIB_MANIFOLD_VERSION"

        annotationProcessor "systems.manifold:manifold:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-preprocessor:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-exceptions:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-strings:$LIB_MANIFOLD_VERSION"
        annotationProcessor "systems.manifold:manifold-ext:$LIB_MANIFOLD_VERSION"
    }
}