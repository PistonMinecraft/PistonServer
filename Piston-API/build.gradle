plugins {
    id 'java-library'
}

archivesBaseName = 'piston-api'

sourceSets {
    pluginApi {
        java.srcDirs =  ['src/pluginapi/java']
        resources.srcDirs =  ['src/pluginapi/resources']
        compileClasspath += main.output
    }
    moddingApi {
        java.srcDirs =  ['src/moddingapi/java']
        resources.srcDirs =  ['src/moddingapi/resources']
        compileClasspath += main.output
    }
    main {
        java.srcDirs = ['src/common/java']
        resources.srcDirs = ['src/common/resources']
    }
}

configurations {
    nonTransitiveImpl {
        transitive = false
    }
    pluginApiAnnotationProcessor.extendsFrom annotationProcessor
    moddingApiAnnotationProcessor.extendsFrom annotationProcessor
    expose {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api
    }
    implementation {
        canBeResolved = true
        extendsFrom(nonTransitiveImpl)
    }
    pluginApiImplementation {
        canBeResolved = true
        extendsFrom(implementation)
    }
    moddingApiImplementation {
        canBeResolved = true
        extendsFrom(implementation)
    }
}

dependencies {
    moddingApiImplementation('cpw.mods:modlauncher:8.0.9') {
        exclude group: 'org.ow2.asm'
        exclude module: 'jopt-simple'
    }

    api 'org.slf4j:slf4j-api:2.0.0-alpha1'
    api 'net.kyori:adventure-api:4.7.0'
    api 'org.joml:joml:1.10.1'

    runtimeOnly 'net.jodah:typetools:0.8.3' // EventBus lib
    nonTransitiveImpl 'net.minecraftforge:eventbus:4.0.0'

    implementation 'org.snakeyaml:snakeyaml-engine:2.2.1'
}

task pluginApiJar(type: Jar) {
    archiveClassifier = 'pluginapi'
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from sourceSets.pluginApi.output
    from sourceSets.main.output
    from configurations.pluginApiImplementation.collect {zipTree(it) }
    from configurations.implementation.collect {zipTree(it) }
}

task moddingApiJar(type: Jar) {
    archiveClassifier = 'moddingapi'
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from sourceSets.moddingApi.output
    from sourceSets.main.output
    from configurations.moddingApiImplementation.collect {zipTree(it) }
    from configurations.implementation.collect {zipTree(it) }
}

jar { // This will gen uber jar
    dependsOn pluginApiJar, moddingApiJar
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from sourceSets.pluginApi.output
    from sourceSets.moddingApi.output
    from configurations.moddingApiImplementation.collect {zipTree(it) }
    from configurations.pluginApiImplementation.collect {zipTree(it) }
    from configurations.implementation.collect {zipTree(it) }
}

artifacts {
    expose pluginApiJar
    expose moddingApiJar
    expose jar
}