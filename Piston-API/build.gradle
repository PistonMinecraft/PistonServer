plugins {
    id 'java-library'
}

archivesBaseName = 'piston-api'
version = "${rootProject.ext.MC_VERSION}-${rootProject.ext.VERSION}+${rootProject.ext.BUILD}"

sourceSets {
    pluginApi {
        java.srcDirs =  ['src/pluginapi/java']
        resources.srcDirs =  ['src/pluginapi/resources']
        compileClasspath += main.compileClasspath
    }
    moddingApi {
        java.srcDirs =  ['src/moddingapi/java']
        resources.srcDirs =  ['src/moddingapi/resources']
        compileClasspath += main.compileClasspath
    }
    main {
        java.srcDirs = ['src/common/java']
        resources.srcDirs = ['src/common/resources']
    }
}

configurations {
    expose {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api
    }
    pluginApi
    moddingApi
    api.extendsFrom pluginApi, moddingApi
}

dependencies {
    pluginApi 'net.md-5:bungeecord-chat:1.16-R0.5-SNAPSHOT'

    moddingApi 'cpw.mods:modlauncher:8.0.9:api'

    api 'org.slf4j:slf4j-api:2.0.0-alpha1'

    implementation 'org.snakeyaml:snakeyaml-engine:2.2.1'
}

task pluginApiJar(type: Jar) {
    archiveClassifier = 'pluginapi'
    from sourceSets.pluginApi.output
    from sourceSets.main.output
    from configurations.pluginApi.collect {zipTree(it) }
}

task moddingApiJar(type: Jar) {
    archiveClassifier = 'moddingapi'
    from sourceSets.moddingApi.output
    from sourceSets.main.output
    from configurations.moddingApi.collect {zipTree(it) }
}

jar { // This will gen uber jar
    archiveClassifier = 'all'
    from sourceSets.pluginApi.output
    from sourceSets.moddingApi.output
//    from configurations.collect {}
}

artifacts {
    expose pluginApiJar
    expose moddingApiJar
    expose jar
}