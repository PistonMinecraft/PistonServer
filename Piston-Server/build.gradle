import org.pistonmc.build.task.ApplyPatchesTask
import org.pistonmc.build.task.GenDecompiledSourcesTask
import org.pistonmc.build.task.GenPatchesTask
import org.pistonmc.build.task.GenProcessedVanillaServerJarTask

import java.nio.file.Files
import java.util.jar.Attributes.Name

ext.BUKKIT_VERSION = "${rootProject.ext.MC_VERSION}-R0.1-SNAPSHOT"

task genProcessedJar(type: GenProcessedVanillaServerJarTask) {
    accessTransformers = [
            file('src/main/resources/access.at').toPath()
    ]
    forceProcess = false
}

task genDecompiledSources(type: GenDecompiledSourcesTask) {
    dependsOn genProcessedJar
    inputJar = genProcessedJar.outputPath
    forceGen = false
}

task genPatches(type: GenPatchesTask) {
    dependsOn genDecompiledSources
    inputSourcesDir = sourceSets.main.java.srcDirs.iterator().next().toPath()
    outputPatchesDir = project.projectDir.toPath().resolve('MC-Patches')
}

task applyPatches(type: ApplyPatchesTask) {
    dependsOn genDecompiledSources
    inputPatchesDir = projectDir.toPath().resolve('MC-Patches')
    outputSourceDir = sourceSets.main.java.srcDirs.iterator().next().toPath()
}

archivesBaseName = 'piston-server'

repositories {
    maven {
        name 'Minecraft Libraries'
        url "https://libraries.minecraft.net"
    }
    maven {
        name 'paper-repo'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name 'sponge-repo'
        url 'https://repo-new.spongepowered.org/repository/maven-public'
    }
}

configurations {
    nonTransitiveImpl {
        transitive = false
    }
    forgeImplementation
    implementation {
        canBeResolved = true
        extendsFrom(forgeImplementation, nonTransitiveImpl)
    }
}

checkerFramework {
    extraJavacArgs = [
            '-AskipDefs=^(net\\.minecraft\\.|com\\.mojang\\.)',
            '-AskipUses=^(net\\.minecraft\\.|com\\.mojang\\.)'
    ]
}

dependencies {
    if(Files.exists(genProcessedJar.outputPath)) {
        implementation files(genProcessedJar.outputPath)
    }
    implementation("com.destroystokyo.paper:paper-api:$BUKKIT_VERSION") {
        exclude group: 'org.ow2.asm'
        exclude group: 'org.ow2.asm'
        exclude module: 'jsr305'
    }
    implementation('org.spongepowered:spongeapi:8.0.0-SNAPSHOT') {
        exclude group: 'org.checkerframework'
    }
    implementation project(path: ':Piston-API', configuration: 'expose')

    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'

    implementation libs.bundles.log4j
    implementation 'com.lmax:disruptor:3.4.2'

    implementation 'io.netty:netty-all:4.1.60.Final'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2' // Other APIs need this
    nonTransitiveImpl 'cpw.mods:grossjava9hacks:+'
    nonTransitiveImpl 'cpw.mods:modlauncher:8.0.9'
    runtimeOnly 'org.apache.maven:maven-artifact:3.6.3' // ForgeSPI lib
    nonTransitiveImpl 'net.minecraftforge:forgespi:3.2.0'
    nonTransitiveImpl 'net.minecraftforge:coremods:4.0.6'
    runtimeOnly 'org.antlr:antlr4-runtime:4.9.1' // AccessTransformers lib
    nonTransitiveImpl 'net.minecraftforge:accesstransformers:3.0.1'

    implementation 'net.minecrell:terminalconsoleappender:1.2.0'
    implementation libs.bundles.jline
}

jar {
    manifest {
        attributes([
                (Name.MAIN_CLASS.toString()): 'org.pistonmc.main.PreLaunch',
                (Name.IMPLEMENTATION_VENDOR.toString()): 'PistonMC',
                (Name.IMPLEMENTATION_TITLE.toString()): 'Piston',
                (Name.IMPLEMENTATION_VERSION.toString()): rootProject.ext.VERSION,
                'Bukkit-Version': BUKKIT_VERSION,
                'Multi-Release': 'true',
                'Server-Launch-Args': '--gameDir . --launchTarget pistonserver'
        ])
//        configurations.implementation.each { dep ->
//            from(project.zipTree(dep)) {
//                exclude 'module-info.class'
//                if(dep.name.contains('server_processed')) {
//                    exclude 'log4j2.xml', 'org/apache/logging/', 'joptsimple/', 'META-INF/', 'io/netty', 'it/unimi/dsi/fastutil/', 'javax/annotation'
//                }
//            }
//        }
    }
}